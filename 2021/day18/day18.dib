#!fsharp

open System.IO
open System.Text.RegularExpressions

type Snailfish =
| Pair of Snailfish*Snailfish
| Number of int

let rec parseLine l = 
    let readNumber l =
        let s = Seq.takeWhile (fun c -> c <> ',' && c <> ']') l
        let n = 
            s |> Seq.map Char.ToString
            |> String.concat ""
            |> int |> Number
        (n, Seq.skip (Seq.length s) l)
    
    if '[' = Seq.head l
    then 
        let (first, l') = parseLine (Seq.tail l) 
        let (second, l'') = parseLine (Seq.tail l')
        Pair (first,second), Seq.tail l''
    else readNumber l

let input = 
    File.ReadAllLines("input.txt")
    |> Array.map (parseLine >> fst)

#!fsharp

let rec getSnail root path =
    match root, path with
    | s, [] -> Some s
    | Pair (l,r), p :: ps -> if p then getSnail r ps else getSnail l ps
    | _ , _ -> None

let dropLast l = l |> List.rev |> List.tail |> List.rev

let rec setSnail root path value =
    match path, root with
    | [], _ -> value
    | p :: ps, Pair (l,r) when p -> Pair (l, setSnail r ps value)
    | p :: ps, Pair (l,r) -> Pair (setSnail l ps value, r)
    | _, _ -> root

let traverse direction root path =
    let up = path |> List.rev |> List.skipWhile ((=) direction) |> List.rev
    let rec down direction root path =
        match getSnail root path with
        | Some (Pair (l,r)) -> down direction root (List.append path [direction])
        | Some (Number n) -> Some path
        | None -> None
    match up with
    | [] -> None
    | up -> down (not direction) root (List.append (dropLast up) [direction])

let addToNumber root path i =
    let newS =
        match getSnail root path with
        | Some (Number n) -> Number (i+n)
        | Some (Pair (l,r)) -> Number i
        | None -> Number i
    setSnail root path newS

let getNumber s dir =
    match s with
    | Number n -> n
    | Pair (l, Number n) when dir -> n
    | Pair (Number n, r) -> n
    | _ -> 0

let explode root path = 
    let s = 
        match getSnail root path with
        | Some r -> r
        | None -> root
    let left = traverse false root path
    let right = traverse true root path
    let l' = 
        match left with
        | Some p -> addToNumber root p (getNumber s false)
        | None -> root
    let r' =
        match right with
        | Some p -> addToNumber l' p (getNumber s true)
        | None -> l'
    setSnail r' path (Number 0)

let split root path =
    match getSnail root path with
    | Some (Number n) -> setSnail root path (Pair (Number (n/2), Number (n / 2 + (n % 2)))) 
    | Some (Pair (l,r)) -> root
    | None -> root 

let rec reduce root path s = 
    let rec explodeRec root path s = 
        match path, s with
        | path, Pair (l,r) when (List.length path) >= 4 -> explode root path |> Some
        | _, Pair (l, r) -> 
            let l' = explodeRec root (List.append path [false]) l 
            let r' = explodeRec root (List.append path [true]) r 
            if Option.isSome l' then l' else r'
        | _, Number n -> None
    let rec splitRec root path s =
        match path, s with
        | path, Number n when n >= 10 -> split root path |> Some
        | _, Pair (l, r) -> 
            let l' = splitRec root (List.append path [false]) l 
            let r' = splitRec root (List.append path [true]) r 
            if Option.isSome l' then l' else r'
        | _, Number n -> None
    let e = explodeRec root path s
    if Option.isSome e then e else splitRec root path s

let add l r = Pair (l,r)

let addAndReduce s s2 =
    let added = add s s2
    let reduced = Seq.unfold (fun s -> reduce s [] s |> Option.bind (fun s -> Some (s,s))) added
    if Seq.isEmpty reduced then added else Seq.last reduced

let rec snailSum s =
    match s with
    | Number n -> n
    | Pair (l,r) -> 3*(snailSum l) + 2*(snailSum r)

Array.reduce addAndReduce input
|> snailSum

#!fsharp

input |> Array.toSeq
|> fun l -> Seq.allPairs l l
|> Seq.filter (fun (a,b) -> a<>b)
|> Seq.map (fun (s1,s2) -> addAndReduce s1 s2 |> snailSum)
|> Seq.max
